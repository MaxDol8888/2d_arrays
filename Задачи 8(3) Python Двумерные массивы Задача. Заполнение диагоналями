program FILLDIAG;
(* Заполнить двумерный массив 10х10 по диагоналям : *)
(*  1  2  4  7 11 ..             *)
(*  3  5  8 12   .               *)
(*  6  9 13    .  /              *)
(* 10 14        /     /          *)
(* 15         /     /            *)
(*          L     /              *)
(*              L             91 *)
(*                    .    92 95 *)
(*                  .   93 96 98 *)
(*                .. 94 97 99100 *)
(* Eugene V. Ogurtsov *)
(* 02.03.2000 *)

(* Variables definitions part *)
var
  Mas : array [1..10, 1..10] of Integer;
  k : Integer;    (* Номер текущей диагонали *)
  x, y : Integer; (* Координаты текущего элемента массива *)
  i : Integer;    (* Вставляемое число *)

(* Operators part *)
begin
  WriteLn;
  WriteLn('Заполнение массива по диагоналям');
  i := 1; (* Начинаем заполнять с 1 *)

  (* Заполнение лево-верхней половины *)
  for k := 1 to 10 do
  begin
    x := k; (* х-координата первого элемента к-ой диагонали *)
    y := 1; (* у-координата первого элемента к-ой диагонали *)
    (* Пока не кончилась диагональ, заполняем ее *)
    while x > 0 do
    begin
      Mas[x, y] := i;
      i := i + 1;
      x := x - 1; (* Смещаемся влево *)
      y := y + 1  (* и вниз *)
    end
  end;

  (* Заполнение право-нижней половины *)
  for k := 2 to 10 do
  begin
    x := 10; (* х-координата первого элемента к-ой диагонали *)
    y := k;  (* у-координата первого элемента к-ой диагонали *)
    (* Пока не кончилась диагональ, заполняем ее *)
    while y <= 10 do
    begin
      Mas[x, y] := i;
      i := i + 1;
      x := x - 1; (* Смещаемся влево *)
      y := y + 1  (* и вниз *)
    end
  end;

  (* Вывод содержимого массива на экран *)
  for y := 1 to 10 do
  begin
    (* Выводим все элементы одной строки (по 3 позиции на число) *)
    for x := 1 to 10 do
      Write(Mas[x, y] : 3);
    (* Переходим к следующей строке *)
    WriteLn
  end
end. (* End of operators part *)

(* End of 'FILLDIAG.PAS' file *)

http://www.school30.spb.ru/csd/ogt/samp/pas_task_021.htm


http://cppstudio.com/post/4991/ - Заполнить двумерный массив зигзагом по диагонали
